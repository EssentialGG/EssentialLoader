import org.apache.commons.io.IOUtils

import java.nio.file.Files
import java.nio.file.StandardCopyOption
import java.nio.file.StandardOpenOption
import java.util.function.Supplier
import java.util.zip.ZipEntry
import java.util.zip.ZipFile
import java.util.zip.ZipOutputStream

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

repositories {
    maven { url "https://maven.minecraftforge.net/" }
    maven { url "https://repo.spongepowered.org/maven" }
}

sourceSets {
    exampleMod
    example2Mod
    oldKotlinMod
    essential
    dummyStage1
    dummyStage2
    dummyStage3
    minecraft10808
    minecraft11202
}

configurations {
    forge10808Runtime
    forge11202Runtime
    minecraft10808CompileOnly.extendsFrom(forge10808Runtime)
    minecraft11202CompileOnly.extendsFrom(forge11202Runtime)

    example2ModImplementation.extendsFrom(exampleModImplementation)
    example2ModCompileOnly.extendsFrom(exampleModCompileOnly)

    mixin07
    mixin08
    asm52
}

dependencies {
    def launchwrapper = "net.minecraft:launchwrapper:1.12"
    // Intentionally using a different version than stage1 to make sure it can determine the version correctly
    def forge = "net.minecraftforge:forge:1.8.8-11.15.0.1655:universal"

    implementation(project.parent.project("common"))
    implementation("commons-codec:commons-codec:1.9")

    // Versions based on the one which MC include by default in 1.8.9 (minimal supported version)
    // See https://github.com/MultiMC/meta-multimc/blob/master/net.minecraft/1.8.9.json
    forge10808Runtime(launchwrapper)
    forge10808Runtime("org.apache.commons:commons-lang3:3.3.2")
    forge10808Runtime("commons-io:commons-io:2.4")
    forge10808Runtime("commons-codec:commons-codec:1.9")
    forge10808Runtime("org.apache.logging.log4j:log4j-api:2.0-beta9")
    forge10808Runtime("org.apache.logging.log4j:log4j-core:2.0-beta9")
    forge10808Runtime("com.google.code.gson:gson:2.2.4")
    forge10808Runtime("com.google.guava:guava:17.0")
    forge10808Runtime("com.mojang:authlib:1.5.21")
    // Forge
    forge10808Runtime(forge)
    forge10808Runtime("lzma:lzma:0.0.1")

    // Versions based on the one which MC (on MultiMC) includes by default in 1.12.2
    // See https://github.com/MultiMC/meta-multimc/blob/master/net.minecraft/1.12.2.json
    forge11202Runtime(launchwrapper)
    forge11202Runtime("org.apache.commons:commons-lang3:3.5")
    forge11202Runtime("commons-io:commons-io:2.5")
    forge11202Runtime("commons-codec:commons-codec:1.10")
    forge11202Runtime("org.apache.logging.log4j:log4j-api:2.8.1")
    forge11202Runtime("org.apache.logging.log4j:log4j-core:2.8.1")
    forge11202Runtime(implementation("com.google.code.gson:gson:2.8.0"))
    forge11202Runtime("com.google.guava:guava:21.0")
    forge11202Runtime("com.mojang:authlib:1.5.25")
    // Forge
    forge11202Runtime("net.minecraftforge:forge:1.12.2-14.23.5.2860:universal")
    forge11202Runtime("lzma:lzma:0.0.1")
    forge11202Runtime("org.apache.maven:maven-artifact:3.5.3")

    // Mixin 0.8
    runtimeOnly("org.ow2.asm:asm-commons:6.2")
    runtimeOnly("com.google.guava:guava:21.0")
    // ArchLoom-based launching in dev
    implementation("net.fabricmc:dev-launch-injector:0.2.1+build.8")

    exampleModImplementation(project(":stage0:launchwrapper"))
    exampleModCompileOnly(launchwrapper)
    exampleModCompileOnly(forge)
    exampleModCompileOnly("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }

    mixin07("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }
    mixin08("org.spongepowered:mixin:0.8.2") {
        transitive = false
    }
    asm52("org.ow2.asm:asm-debug-all:5.2")

    oldKotlinModCompileOnly(forge)

    essentialCompileOnly(launchwrapper)
    essentialCompileOnly(forge)

    dummyStage1CompileOnly(launchwrapper)
    dummyStage2CompileOnly(launchwrapper)
    dummyStage3CompileOnly(launchwrapper)
}

def integrationTest = tasks.register("integrationTest", Test) {
    testClassesDirs = sourceSets.main.output.classesDirs
    classpath = sourceSets.main.runtimeClasspath

    useJUnitPlatform()

    inputs.dir(tasks.setupDownloadsApi.destinationDir)
    dependsOn(tasks.setupDownloadsApi)
}
check.dependsOn(integrationTest)

def includeMixin(AbstractArchiveTask task, Configuration mixin) {
    task.dependsOn(mixin)
    task.from({ mixin.collect { zipTree(it) } }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

def configureExampleModJar = { String tweaker, Configuration mixin = null -> return { AbstractArchiveTask task ->
    archiveBaseName.set(task.name)
    from(sourceSets.exampleMod.output)
    dependsOn(configurations.exampleModRuntimeClasspath)
    from({ configurations.exampleModRuntimeClasspath.collect { zipTree(it) } })

    manifest {
        attributes "FMLCorePlugin": "com.example.mod.ExampleCoreMod",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "ForceLoadAsMod": "true",
                "TweakClass": tweaker,
                "TweakOrder": "0"
    }

    if (mixin != null) {
        includeMixin(task, mixin)
        manifest.attributes "MixinConfigs": "examplemod.mixins.json,examplemod.init.mixins.json"
    }
} }

def configureExample2ModJar = { String tweaker, Configuration mixin = null -> return { AbstractArchiveTask task ->
    archiveBaseName.set(name)
    from(sourceSets.example2Mod.output)
    dependsOn(configurations.example2ModRuntimeClasspath)
    from({ configurations.example2ModRuntimeClasspath.collect { zipTree(it) } })

    manifest {
        attributes "FMLCorePlugin": "com.example.mod2.ExampleCoreMod",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "ForceLoadAsMod": "true",
                "TweakClass": tweaker,
                "TweakOrder": "0"
    }

    if (mixin != null) {
        includeMixin(task, mixin)
        manifest.attributes "MixinConfigs": "example2mod.mixins.json"
    }
} }

tasks.register("exampleModJar", Jar, configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker"))
tasks.register("example2ModJar", Jar, configureExample2ModJar("com.example.mod2.tweaker.ExampleModTweaker"))
tasks.register("exampleModEssentialTweakerJar", Jar, configureExampleModJar("gg.essential.loader.stage0.EssentialSetupTweaker"))
tasks.register("example2ModEssentialTweakerJar", Jar, configureExample2ModJar("gg.essential.loader.stage0.EssentialSetupTweaker"))

tasks.register("exampleModWithMixin07Jar", Jar, configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker", configurations.mixin07))
tasks.register("example2ModWithMixin07Jar", Jar, configureExample2ModJar("com.example.mod2.tweaker.ExampleModTweaker", configurations.mixin07))
tasks.register("exampleModEssentialTweakerWithMixin07Jar", Jar, configureExampleModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin07))
tasks.register("example2ModEssentialTweakerWithMixin07Jar", Jar, configureExample2ModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin07))
tasks.register("exampleModMixinTweakerWithMixin07Jar", Jar, configureExampleModJar("org.spongepowered.asm.launch.MixinTweaker", configurations.mixin07))
tasks.register("example2ModMixinTweakerWithMixin07Jar", Jar, configureExample2ModJar("org.spongepowered.asm.launch.MixinTweaker", configurations.mixin07))

tasks.register("exampleModWithMixin08Jar", Jar, configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker", configurations.mixin08))
tasks.register("example2ModWithMixin08Jar", Jar, configureExample2ModJar("com.example.mod2.tweaker.ExampleModTweaker", configurations.mixin08))
tasks.register("exampleModEssentialTweakerWithMixin08Jar", Jar, configureExampleModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin08))
tasks.register("example2ModEssentialTweakerWithMixin08Jar", Jar, configureExample2ModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin08))
tasks.register("exampleModMixinTweakerWithMixin08Jar", Jar, configureExampleModJar("org.spongepowered.asm.launch.MixinTweaker", configurations.mixin08))
tasks.register("example2ModMixinTweakerWithMixin08Jar", Jar, configureExample2ModJar("org.spongepowered.asm.launch.MixinTweaker", configurations.mixin08))

tasks.register("exampleRelocatedModJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    def configure = configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker")
    configure.delegate = delegate
    configure(it)
    relocate("gg.essential.loader.stage0", "com.example.mod.essential.stage0")
}

(1..5).each { i ->
    def stage2Task = tasks.register("stage2V${i}Jar", Jar) {
        archiveBaseName.set(name)
        from(evaluationDependsOn(':stage2:launchwrapper').tasks.jar.archiveFile.map { zipTree(it) })
        // Dummy attribute so they all have different hashes
        manifest {
            attributes "Implementation-Version": "$i"
        }
    }
    def stage3Task = tasks.register("stage3V${i}Jar", Jar) {
        archiveBaseName.set(name)
        from(tasks.essentialJar.archiveFile.map { zipTree(it) })
        manifest {
            // Dummy attribute so they all have different hashes
            attributes "Implementation-Version": "$i"
            // For stage3 version 4+, we add an explicit requirement on stage2 version 4
            if (i >= 4) {
                attributes "Requires-Essential-Stage2-Version": "4"
            }
        }
    }
    tasks.register("exampleBundledModJar$i", Jar) {
        def configure = configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker")
        configure.delegate = delegate
        configure(it)

        def stage2Jar = stage2Task.get().archiveFile
        def stage3Jar = stage3Task.get().archiveFile
        from(stage2Jar) {
            rename { "bundled-stage2-${i}.jar" }
        }
        from(stage3Jar) {
            rename { "bundled-essential-${i}.jar" }
        }
        from(file("../essential-loader-stage2.properties")) {
            expand(["pinnedFileMd5": { stage2Jar.get().asFile.bytes.md5() }, version: i])
        }
        from(file("../essential-loader.properties")) {
            expand(["pinnedFileMd5": { stage3Jar.get().asFile.bytes.md5() }, "version": i])
        }
    }
}

tasks.register("oldKotlinModJar", Jar) {
    archiveBaseName.set("oldKotlinMod")
    from(sourceSets.oldKotlinMod.output)
    manifest {
        attributes "FMLCorePlugin": "com.example.oldkotlin.CoreMod"
    }
}

tasks.register("essentialJar", Jar) {
    archiveBaseName.set("essential")
    from(sourceSets.essential.output)
    includeMixin(it, configurations.mixin07)
}

tasks.register("essentialWithMixin08Jar", Jar) {
    archiveBaseName.set("essential-mixin08")
    from(sourceSets.essential.output)
    includeMixin(it, configurations.mixin08)
}

tasks.register("essentialWithAsm52Jar", Jar) {
    archiveBaseName.set("essential-asm52")
    from(sourceSets.essential.output)
    includeMixin(it, configurations.mixin07)

    dependsOn(configurations.asm52)
    from({ configurations.asm52.collect { zipTree(it) } })
}

tasks.register("dummyStage1Jar", Jar) {
    archiveBaseName.set("dummyStage1")
    from(sourceSets.dummyStage1.output)
    manifest {
        attributes("Name": "gg/essential/loader/stage1/")
        attributes("Implementation-Version": 999999)
    }
}

tasks.register("dummyStage2Jar", Jar) {
    archiveBaseName.set("dummyStage2")
    from(sourceSets.dummyStage2.output)
}

tasks.register("dummyStage3Jar", Jar) {
    archiveBaseName.set("dummyStage3")
    from(sourceSets.dummyStage3.output)
    includeMixin(it, configurations.mixin07)
}

tasks.register("dummyStage3DiffJar", Jar) {
    archiveBaseName.set("dummyStage3Diff")
    from(essentialJar.archiveFile.map { zipTree(it) }) {
        into("-")
    }
    from(dummyStage3Jar.archiveFile.map { zipTree(it) }) {
        into("~")
    }
}

tasks.register("forge10808Jar", Jar) {
    archiveBaseName.set("forge-10808")

    from(sourceSets.minecraft10808.output)
    dependsOn(configurations.forge10808Runtime)
    from({ configurations.forge10808Runtime.collect { zipTree(it) } }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
        exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
        exclude 'net/minecraftforge/fml/common/launcher/TerminalTweaker.class'
    }
}

tasks.register("forge11202Jar", Jar) {
    archiveBaseName.set("forge-11202")

    from(sourceSets.minecraft11202.output)
    dependsOn(configurations.forge11202Runtime)
    from({ configurations.forge11202Runtime.collect { zipTree(it) } }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
        exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
        exclude 'net/minecraftforge/fml/common/launcher/TerminalTweaker.class'
    }
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    doLast {
        // A safe, constant value for creating consistent zip entries
        // From: https://github.com/gradle/gradle/blob/d6c7fd470449a59fc57a26b4ebc0ad83c64af50a/subprojects/core/src/main/java/org/gradle/api/internal/file/archive/ZipCopyAction.java#L42-L57
        def CONSTANT_TIME_FOR_ZIP_ENTRIES = new GregorianCalendar(1980, Calendar.FEBRUARY, 1, 0, 0, 0).getTimeInMillis();

        def path = archiveFile.get().asFile.toPath()
        def tmpPath = Files.createTempFile(path.parent, "tmp", ".jar")
        try {
            try (OutputStream fileOut = Files.newOutputStream(tmpPath, StandardOpenOption.TRUNCATE_EXISTING);
                 ZipOutputStream zipOut = new ZipOutputStream(fileOut);
                 ZipFile zipIn = new ZipFile(path.toFile())) {
                List<? extends ZipEntry> entries = Collections.list(zipIn.entries())
                entries.sort(Comparator.comparing(ZipEntry::getName))
                for (ZipEntry entry : entries) {
                    ZipEntry newEntry = new ZipEntry(entry.getName())
                    newEntry.setTime(CONSTANT_TIME_FOR_ZIP_ENTRIES)
                    zipOut.putNextEntry(newEntry)
                    IOUtils.copy(zipIn.getInputStream(entry), zipOut)
                }
            }
            Files.move(tmpPath, path, StandardCopyOption.REPLACE_EXISTING)
        } finally {
            Files.deleteIfExists(tmpPath);
        }
    }
}

tasks.register("setupDownloadsApi", Sync) {
    def downloadsApi = new File(project.buildDir, "downloadsApi")
    into(downloadsApi)

    def nextId = 0
    def platformMod = { Provider<RegularFile> source, String mod, String branch, String platform ->
        def id = nextId++
        def v1Json = "v1/$mod/versions/$branch/platforms/$platform"
        def v1Download = "$v1Json/download"
        def dstJar = "${v1Json}.jar"

        def jarFile = new File(downloadsApi, dstJar).absoluteFile
        preserve.include(dstJar)
        from(source) {
            rename { dstJar }
        }

        def json = { String endpoint, Supplier<String> content ->
            def endpointFile = endpoint + ".json"
            preserve.include(endpointFile)
            doLast {
                def file = new File(downloadsApi, endpointFile)
                file.getParentFile().mkdirs()
                file.write(content.get())
            }
        }
        json(v1Json, { """{ "version": "$branch", "checksum": "${jarFile.bytes.md5()}", "id": "$id" }""" })
        json(v1Download, { """{ "url": "${jarFile.toURI()}", "checksum": "${jarFile.bytes.md5()}" }""" })
    }
    def diff = { Provider<RegularFile> source, String mod, String branch, String toBranch, String platform ->
        def v1Json = "v1/$mod/versions/$branch/diff/$toBranch/platforms/$platform"
        def dstJar = "${v1Json}.jar"

        def jarFile = new File(downloadsApi, dstJar).absoluteFile
        preserve.include(dstJar)
        from(source) {
            rename { dstJar }
        }

        def json = { String endpoint, Supplier<String> content ->
            def endpointFile = endpoint + ".json"
            preserve.include(endpointFile)
            doLast {
                def file = new File(downloadsApi, endpointFile)
                file.getParentFile().mkdirs()
                file.write(content.get())
            }
        }
        json(v1Json, { """{ "url": "${jarFile.toURI()}", "checksum": "${jarFile.bytes.md5()}" }""" })
    }
    def platformForge10808 = "forge_1-8-8"
    def platformForge11202 = "forge_1-12-2"
    def mod = { Provider<RegularFile> source, String mod, String branch ->
        platformMod(source, mod, branch, platformForge10808)
        platformMod(source, mod, branch, platformForge11202)
    }

    mod(tasks.exampleModJar.archiveFile, "example:mod", "stable")
    mod(tasks.example2ModJar.archiveFile, "example:mod2", "stable")
    mod(tasks.exampleModEssentialTweakerJar.archiveFile, "example:mod", "essential-tweaker")
    mod(tasks.example2ModEssentialTweakerJar.archiveFile, "example:mod2", "essential-tweaker")

    mod(tasks.exampleModWithMixin07Jar.archiveFile, "example:mod", "stable-with-mixin-07")
    mod(tasks.example2ModWithMixin07Jar.archiveFile, "example:mod2", "stable-with-mixin-07")
    mod(tasks.exampleModEssentialTweakerWithMixin07Jar.archiveFile, "example:mod", "essential-tweaker-with-mixin-07")
    mod(tasks.example2ModEssentialTweakerWithMixin07Jar.archiveFile, "example:mod2", "essential-tweaker-with-mixin-07")
    mod(tasks.exampleModMixinTweakerWithMixin07Jar.archiveFile, "example:mod", "mixin-tweaker-with-mixin-07")
    mod(tasks.example2ModMixinTweakerWithMixin07Jar.archiveFile, "example:mod2", "mixin-tweaker-with-mixin-07")

    mod(tasks.exampleModWithMixin08Jar.archiveFile, "example:mod", "stable-with-mixin-08")
    mod(tasks.example2ModWithMixin08Jar.archiveFile, "example:mod2", "stable-with-mixin-08")
    mod(tasks.exampleModEssentialTweakerWithMixin08Jar.archiveFile, "example:mod", "essential-tweaker-with-mixin-08")
    mod(tasks.example2ModEssentialTweakerWithMixin08Jar.archiveFile, "example:mod2", "essential-tweaker-with-mixin-08")
    mod(tasks.exampleModMixinTweakerWithMixin08Jar.archiveFile, "example:mod", "mixin-tweaker-with-mixin-08")
    mod(tasks.example2ModMixinTweakerWithMixin08Jar.archiveFile, "example:mod2", "mixin-tweaker-with-mixin-08")

    mod(tasks.exampleRelocatedModJar.archiveFile, "example:mod", "relocated")
    (1..5).each { i ->
        mod(tasks.named("exampleBundledModJar$i").map { it.archiveFile }, "example:mod", "bundled-$i")
        mod(tasks.named("stage2V${i}Jar").map { it.archiveFile }, "essential:loader-stage2", "$i")
        mod(tasks.named("stage3V${i}Jar").map { it.archiveFile }, "essential:essential", "$i")
    }

    mod(tasks.oldKotlinModJar.archiveFile, "example:kotlin", "old")

    mod(evaluationDependsOn(':stage2:launchwrapper').tasks.jar.archiveFile, "essential:loader-stage2", "stable")
    mod(tasks.essentialJar.archiveFile, "essential:essential", "stable")
    mod(tasks.essentialWithMixin08Jar.archiveFile, "essential:essential", "mixin-08")
    mod(tasks.essentialWithAsm52Jar.archiveFile, "essential:essential", "asm-52")
    mod(provider { configurations.mixin07.singleFile }, "essential:mixin", "07")
    mod(evaluationDependsOn(':stage0:launchwrapper').tasks.jar.archiveFile, "essential:loader-stage0", "stable")
    mod(tasks.dummyStage1Jar.archiveFile, "essential:loader-stage1", "dummy")
    mod(tasks.dummyStage2Jar.archiveFile, "essential:loader-stage2", "dummy")
    mod(tasks.dummyStage3Jar.archiveFile, "essential:essential", "dummy")
    diff(tasks.dummyStage3DiffJar.archiveFile, "essential:essential", "stable", "dummy", platformForge10808)

    platformMod(tasks.forge10808Jar.archiveFile, "forge:runtime", "stable", platformForge10808)
    platformMod(tasks.forge11202Jar.archiveFile, "forge:runtime", "stable", platformForge11202)
}
