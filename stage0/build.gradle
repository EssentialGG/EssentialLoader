version = "1.3.1"

interface Injected {
    @javax.inject.Inject ArchiveOperations getArchiveOps()
}

configure(subprojects.findAll { it.name != "common" && it.name != "modlauncher" }) {
    apply plugin: 'maven-publish'

    version = parent.version

    def variant = project.name

    configurations {
        bundle
    }

    dependencies {
        bundle(compileOnly(parent.project("common")))
        if (variant.startsWith("modlauncher")) {
            bundle(compileOnly(parent.project("modlauncher")))
        }
    }

    def injected = objects.newInstance(Injected.class)
    def archiveOps = injected.getArchiveOps()

    jar {
        dependsOn configurations.bundle
        from { configurations.bundle.collect { archiveOps.zipTree(it) } }

        from(evaluationDependsOn(":stage1:$variant").tasks.named("jar").map { it.archiveFile }) {
            rename { "gg/essential/loader/stage0/stage1.jar" }
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                artifactId = "loader-$variant"
                artifact jar
            }
        }

        repositories {
            if (project.hasProperty("nexus_user")) {
                maven {
                    name = "nexus"
                    url = "https://repo.essential.gg/repository/maven-releases/"

                    credentials {
                        username = project.nexus_user
                        password = project.nexus_password
                    }
                }
            }
        }
    }
}
